GET_MOD_DIRECTORY_PATH (
	OUT STRING filePath
)
ADD filePath "common\on_actions\00_h4mb_setup_regions_states.txt"

FILE file filePath

LIST export_list STRING
ADD export_list "on_actions = {"
ADD export_list "\ton_startup = {"
ADD export_list "\t\teffect = {"

GET_ALL_REGIONS_IDS (
	OUT LIST all_regions_ids INT
)
GET_SIZE (
	OUT INT all_regions_count
	all_regions_ids
)

STRING line "\t\t\tresize_array = { global.regions_to_states_ids_start_index = "
ADD line all_regions_count
ADD line " }"
ADD export_list line
SET line "\t\t\tresize_array = { global.regions_to_states_ids_end_index = "
ADD line all_regions_count
ADD line " }"
ADD export_list line

ADD export_list "\t\t\t\n"

MAP states_ids_by_regions_ids INT LIST

GET_ALL_STATES_IDS (
	OUT LIST states_ids INT
)

FOR state_id IN states_ids
	GET_STATE_REGION_ID (
		OUT INT state_region_id
		state_id
	)

	IF state_region_id == 0
		CONTINUE
	
	HAS_KEY (
		OUT BOOLEAN is_present
		states_ids_by_regions_ids
		state_region_id
	)
	
	LIST region_states_ids INT
	
	IF is_present == TRUE
		GET (
			OUT region_states_ids
			states_ids_by_regions_ids
			state_region_id
		)
	
	IF is_present == FALSE
		PUT (
			OUT states_ids_by_regions_ids
			state_region_id
			region_states_ids
		)
	
	ADD region_states_ids state_id
	

GET_KEYS (
	OUT LIST regions_ids INT
	states_ids_by_regions_ids
)

SORT regions_ids

INT start_index 0
INT end_index 0

FOR region_id IN regions_ids
	GET (
		OUT LIST states_ids INT
		states_ids_by_regions_ids
		region_id
	)
	GET_SIZE (
		OUT INT states_count
		states_ids
	)
	
	SORT states_ids
	
	IF states_count > 0
		
		SET line "\t\t\tset_variable = { global.regions_to_states_ids_start_index^"
		ADD line region_id
		ADD line " = "
		ADD line start_index
		ADD line " }"
		ADD export_list line
		
		
		SET end_index start_index
		ADD end_index states_count
		
		SET line "\t\t\tset_variable = { global.regions_to_states_ids_end_index^"
		ADD line region_id
		ADD line " = "
		ADD line end_index
		ADD line " }"
		ADD export_list line

		ADD start_index states_count
		
		FOR state_id IN states_ids
			SET line "\t\t\tadd_to_array = { global.regions_to_states_ids = "
			ADD line state_id
			ADD line " }"
			ADD export_list line

		
		ADD export_list "\t\t\t"

ADD export_list "\t\t}"
ADD export_list "\t}"
ADD export_list "}"

WRITE file export_list